/*
 * Copyright (c) 2019-2023 BassSlapper89, QuakeDoomNukem, RichardDS90, Ozymandias81, Tormentor667, Ghastly_Dragon
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
**/

//BUBBLES
Actor Bubble : SimpleActor
{
	Radius 2
	Height 2
	Speed 1
	Alpha 0.9
	RenderStyle Add
	Projectile
	+FORCEXYBILLBOARD
	DistanceCheck "jj_sfxlod"
	States
	{
	Spawn:
		SBUB A 1
	Rise:
		SBUB A 0 A_JumpIf(WaterLevel == 0, "Death")
		SBUB A 1
		SBUB A 0 A_JumpIf(WaterLevel == 0, "Death")
		SBUB B 1
		SBUB A 0 A_JumpIf(WaterLevel == 0, "Death")
		SBUB C 1
		SBUB A 0 A_JumpIf(WaterLevel == 0, "Death")
		SBUB D 1
		SBUB A 0 A_JumpIf(WaterLevel == 0, "Death")
		SBUB E 1
		SBUB A 0 A_JumpIf(WaterLevel == 0, "Death")
		SBUB F 1
		SBUB A 0 A_JumpIf(WaterLevel == 0, "Death")
		SBUB G 1
		SBUB A 0 A_JumpIf(WaterLevel == 0, "Death")
		SBUB H 1
		SBUB A 0 A_JumpIf(WaterLevel == 0, "Death")
		SBUB I 1
		Loop
	Death:
		TNT1 A 1
		Stop
	}
}

Actor BubbleSpawner
{
	//$Category JJ_Effects
	Radius 2
	Height 2
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 2 A_SpawnItemEx("Bubble", random (-16, 16), 0, 0, 0, 0, 2, random (0, 360), 0, random(128, 224))
		Loop
	}
}

//COMET
Actor CometSpawner
{
	//$Category JJ_Effects
	//$Sprite COMTA0
	//$Arg0 "Radius"
	//$Arg0Tooltip "With 128 for example you get a 256x256 quare or circle (See fourth argument)"
	//$Arg1 "Frequency"
	//$Arg1Tooltip "The lower the number, the heavier the rainfall"
	//$Arg2 "Sound"
	//$Arg2Type 11
	//$Arg2Enum { 0 = "Yes"; 1 = "No"; }
	//$Arg3 "Area Type"
	//$Arg3Type 11
	//$Arg3Enum { 0 = "Square"; 1 = "Circle"; }
	Radius 1
	Height 1
	+NOCLIP
	+NOGRAVITY
	+SPAWNCEILING
	States
	{
	Spawn:
		TNT1 A 0
	Active:
		TNT1 A 0 A_JumpIf(!Args[2], "StartSound")
		Goto CometLoop
	StartSound:
		TNT1 A 0 A_StartSound("FIRELOOP", CHAN_AUTO, CHANF_LOOP, frandom(0.2,0.4))
		Goto CometLoop
	CometLoop:
		TNT1 A 0 A_JumpIf(!Args[3],"Circle")
		TNT1 A 2 A_SpawnItemEx("ChameleonComet", frandom(-Args[0], Args[0]), frandom(-Args[0], Args[0]), -2, 0, 0, 0, 0, 128, Args[1])
		Loop
	Circle:
		TNT1 A 2 A_SpawnItemEx("ChameleonComet", frandom(-Args[0], Args[0]), 0, -2, 0, 0, 0, frandom(0, 360), 128, Args[1])
		Loop
	}
}

Actor ChameleonComet
{
	Height 2
	Radius 2
	Renderstyle Translucent
	Alpha 0.6
	-NOGRAVITY
	+BRIGHT
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+MISSILE
	+NOBLOCKMAP
	DistanceCheck "jj_sfxlod"
	States
	{
	Spawn:
		COMT AAAABBBBCCCC 1
		Loop
	Death:
		SEKE C 3 A_SpawnItemEx("CometDeathGlow", 0, 0, 0, 0, 0, 0, 0, 128)
		SEKE D 3 A_Explode(20,128,0)
		SEKE D 0 A_SpawnItemEx("CometDeath", 0, 0, 0, 0, 0, 0, 0, 128)
		Stop
	}
}

//RAIN
Actor RainDrop
{
	Height 2
	Radius 2
	Alpha 0.6
	-NOGRAVITY
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+MISSILE
	+NOBLOCKMAP
	DistanceCheck "jj_sfxlod"
	States
	{
	Spawn:
		RNDR A 1 A_JumpIf(WaterLevel > 0, "Death")
		Loop
	Death:
		RNDR BCDEFGH 3 A_FadeOut(0.15)
		Stop
	}
}

Actor RainSpawner : SwitchableDecoration
{
	//$Category JJ_Effects
	//$Sprite RNDRA0
	//$Arg0 "Radius"
	//$Arg0Tooltip "With 128 for example you get a 256x256 quare or circle (See fourth argument)"
	//$Arg1 "Frequency"
	//$Arg1Tooltip "The lower the number, the heavier the rainfall"
	//$Arg2 "Sound"
	//$Arg2Type 11
	//$Arg2Enum { 0 = "Yes"; 1 = "No"; }
	//$Arg3 "Area Type"
	//$Arg3Type 11
	//$Arg3Enum { 0 = "Square"; 1 = "Circle"; }
	States
	{
	Spawn:
		TNT1 A 0
	Active:
		TNT1 A 0 A_JumpIf(!Args[2], "StartSound")
		Goto RainLoop
	StartSound:
		TNT1 A 0 A_StartSound("Ambient/Rain", CHAN_7, CHANF_LOOP, frandom(0.7,1.0))
		Goto RainLoop
	RainLoop:
		TNT1 A 0 A_JumpIf(!Args[3],"Circle")
		TNT1 A 2 A_SpawnItemEx("RainDrop", frandom(-Args[0], Args[0]), frandom(-Args[0], Args[0]), -2, 0, 0, 0, 0, 128, Args[1])
		Loop
	Circle:
		TNT1 A 2 A_SpawnItemEx("RainDrop", frandom(-Args[0], Args[0]), 0, -2, 0, 0, 0, frandom(0, 360), 128, Args[1])
		Loop
	}
}

//SNOW
Actor SnowParticle
{
	Radius 1
	Height 1
	Damage 0
	Projectile
	RenderStyle Translucent
	Alpha 0
	Scale 0.6
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+MISSILE
	+NOBLOCKMAP
	DistanceCheck "jj_sfxlod"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetScale(frandom(0.3, 0.6))
		SNOW AAAAAAA 2 A_FadeIn(0.1)
		SNOW A 3
		Goto Spawn+9
	Death:
		SNOW A 1 A_FadeOut(0.05)
		Loop
	}
}

Actor SnowSpawner
{
	//$Category JJ_Effects
	//$Sprite SNOWA0
	//$Arg0 "Radius"
	//$Arg0Tooltip "Radius in map units\nDefault: 0"
	//$Arg1 "Frequency"
	//$Arg1Tooltip "The lower the number, the heavier the snowfall\nRange: 0 - 255"
	//$Arg2 "Area"
	//$Arg2Type 11
	//$Arg2Enum { 0 = "Square"; 1 = "Circle"; }
	Radius 1
	Height 1
	-SOLID
	+NOBLOCKMAP
	+NOGRAVITY
	+NOSECTOR //this means it goes through walls, mind you - ozy81
	+NOINTERACTION
	+NOCLIP
	+SPAWNCEILING
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_CheckSight("Unsighted")
		TNT1 A 0 A_JumpIf(Args[2] > 0, "Circle")
		TNT1 A 8 A_SpawnItemEx("SnowParticle", Random(-Args[0], -Args[0]), Random(-Args[0], -Args[0]), 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, -3.0), 0, 128, Args[1])
		Loop
	Circle:
		TNT1 A 8 A_SpawnItemEx("SnowParticle", Random(-Args[0], -Args[0]), 0, 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, -3.0), Random(0, 360), 128, Args[1])
		Loop
	Unsighted:
		TNT1 A 1
		Goto Spawn
	}
}

//AIRGEMS
Actor AirGemBase
{
	+BRIGHT
	+FLOATBOB
	+NOGRAVITY
}

Actor ElementAirGemProp : AirGemBase
{
	States
	{
	Spawn:
		ELAG ABCDEFGH 3 A_SpawnItemEx("AirSparkle")
		Loop
	}
}

Actor ElementFireGemProp : AirGemBase
{
	States
	{
	Spawn:
		ELFG ABCDEFGH 3 A_SpawnItemEx("FireSparkle")
		Loop
	}
}

Actor ElementWaterGemProp : AirGemBase
{
	States
	{
	Spawn:
		ELWG ABCDEFGH 3 A_SpawnItemEx("WaterSparkle")
		Loop
	}
}

Actor ElementEarthGemProp : AirGemBase
{
	States
	{
	Spawn:
		ELEG ABCDEFGH 3 A_SpawnItemEx("EarthSparkle")
		Loop
	}
}

Actor ElementLightGemProp : AirGemBase
{
	States
	{
	Spawn:
		ELLG ABCDEFGH 3 A_SpawnItemEx("LightSparkle")
		Loop
	}
}

Actor ElementDarkGemProp : AirGemBase
{
	States
	{
	Spawn:
		ELDG ABCDEFGH 3 A_SpawnItemEx("DarkSparkle")
		Loop
	}
}

Actor AirSparkle
{
	States
	{
	Spawn:
		TNT1 AAAA 0 a_spawnparticle(
			/*color*/white,
			/*flags*/ +SPF_FULLBRIGHT +SPF_RELATIVE,
			/*lifetime*/20,
			/*size*/random(3,4),
			/*angle*/ frandom(0,360),
			/*xyz off*/ random(-25,25), random(-25,25), 2,
			/*xyz vel*/ 0, 0, 0,
			/*xyz accel*/ 0, 0, 0,
			/*start alpha*/ 0.7,
			/*fadestep*/ -2)
		ELAG AAAA 0
		Stop
	}
}

Actor WaterSparkle
{
	States
	{
	Spawn:
		TNT1 AAAA 0 a_spawnparticle(
			/*color*/blue,
			/*flags*/ +SPF_FULLBRIGHT +SPF_RELATIVE,
			/*lifetime*/35,
			/*size*/random(3,4),
			/*angle*/ frandom(0,360),
			/*xyz off*/ random(-25,25), random(-25,25), 2,
			/*xyz vel*/ 0, 0, 0,
			/*xyz accel*/ 0, 0, 0,
			/*start alpha*/ 0.7,
			/*fadestep*/ -2)
		ELWG AAAA 0
		Stop
	}
}

Actor EarthSparkle
{
	States
	{
	Spawn:
		TNT1 AAAA 0 a_spawnparticle(
			/*color*/green,
			/*flags*/ +SPF_FULLBRIGHT +SPF_RELATIVE,
			/*lifetime*/35,
			/*size*/random(3,4),
			/*angle*/ frandom(0,360),
			/*xyz off*/ random(-25,25), random(-25,25), 2,
			/*xyz vel*/ 0, 0, 0,
			/*xyz accel*/ 0, 0, 0,
			/*start alpha*/ 0.7,
			/*fadestep*/ -2)
		ELEG AAAA 0
		Stop
	}
}

Actor FireSparkle
{
	States
	{
	Spawn:
		TNT1 AAAA 0 a_spawnparticle(
			/*color*/red,
			/*flags*/ +SPF_FULLBRIGHT +SPF_RELATIVE,
			/*lifetime*/35,
			/*size*/random(3,4),
			/*angle*/ frandom(0,360),
			/*xyz off*/ random(-25,25), random(-25,25), 2,
			/*xyz vel*/ 0, 0, 0,
			/*xyz accel*/ 0, 0, 0,
			/*start alpha*/ 0.7,
			/*fadestep*/ -2)
		ELFG AAAA 0
		Stop
	}
}

Actor LightSparkle
{
	States
	{
	Spawn:
		TNT1 AAAA 0 a_spawnparticle(
			/*color*/yellow,
			/*flags*/ +SPF_FULLBRIGHT +SPF_RELATIVE,
			/*lifetime*/35,
			/*size*/random(3,4),
			/*angle*/ frandom(0,360),
			/*xyz off*/ random(-25,25), random(-25,25), 2,
			/*xyz vel*/ 0, 0, 0,
			/*xyz accel*/ 0, 0, 0,
			/*start alpha*/ 0.7,
			/*fadestep*/ -2)
		ELLG AAAA 0
		Stop
	}
}

Actor DarkSparkle
{
	States
	{
	Spawn:
		TNT1 AAAA 0 a_spawnparticle(
			/*color*/purple,
			/*flags*/ +SPF_FULLBRIGHT +SPF_RELATIVE,
			/*lifetime*/35,
			/*size*/random(3,4),
			/*angle*/ frandom(0,360),
			/*xyz off*/ random(-25,25), random(-25,25), 2,
			/*xyz vel*/ 0, 0, 0,
			/*xyz accel*/ 0, 0, 0,
			/*start alpha*/ 0.7,
			/*fadestep*/ -2)
		ELDG AAAA 0
		Stop
	}
}