Actor FireLizard 3129
{
	//$Category Goons
	Tag "Fire Lizard"
	Radius 12
	Height 56
	Speed 10
	Health 200
	PainChance 0
	Mass 10000
	Damage 20
	MONSTER
	+COUNTKILL
	+DONTFALL
	+DONTHARMSPECIES
	+FLOATBOB
	+NOBLOOD
	+NOGRAVITY
	+NOINFIGHTING
	+SOLID
	SeeSound "LIZRDSEE"
	Obituary "$OB_FIRLZ"
	States
	{
	Spawn:
		FRLZ AB 3 A_Look
		Loop
	See:
		FRLZ AB 3 A_Chase
		Loop
	Missile:
		FRLZ C 2 A_FaceTarget
		FRLZ D 8 BRIGHT A_CustomMissile("FireLizardBall",25,0,359,0)
		Goto See
	Death:
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("JazzPoof",0,0,24,frandom(-8,8),frandom(-8,8),frandom(4,8))
		TNT1 A 0 A_GiveToTarget("Points",300)
		Stop
	Death.JazzprojIce:
		TNT1 A 0 A_GiveToTarget("Points",300)
		FRLZ D 5 A_GenericFreezeDeath
		FRLZ D 1 A_FreezeDeathChunks
		Wait
	Death.JazzprojFire:
	Burn:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_GiveToTarget("Points",300)
		TNT1 A 0 A_PlaySound("FMTRFIRE")
		FRLZ A 2 A_SpawnItemEx("FireDeadSpawner",0,0,0,0,0,0,0,128)
		TNT1 A 2 A_SpawnItemEx("FireDead",0,0,0,0,0,0,0,128)
		TNT1 A 0 A_SpawnItemEx("AshHolm",0,0,0,0,0,0,0,128)
		Stop
	}
}