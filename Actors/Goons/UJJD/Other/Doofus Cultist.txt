Actor ElementGoon3 4035
{
	//$Category Goons
	Tag "Doofus Cultist"
	Health 175
	Radius 20
	Height 56
	Mass 200
	Speed 15
	PainChance 64
	MONSTER
	+COUNTKILL
	+DONTHARMCLASS
	+FLOORCLIP
	+NOBLOOD
	+NOINFIGHTING
	+SOLID
	SeeSound "BIGTURTS"
	PainSound "BIGTURTP"
	ActiveSound "Turtact"
	Obituary "$OB_ELEG2"
	States
	{
	Spawn:
		ELM3 A 10 A_Look
		Loop
	See:
		ELM3 AABBCCDD 3 A_Chase
		Loop
	Missile:
		ELM3 E 20 Bright A_FaceTarget
		TNT1 A 0 A_PlaySound("DNDFIRE")
		ELM3 F 5 Bright A_CustomMissile("Neutronshot",32,0,0,0,0)
		ELM3 E 2 Bright
		ELM3 F 5 Bright A_SpidRefire
		Goto Missile+1
	Death:
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("JazzPoof",0,0,24,frandom(-8,8),frandom(-8,8),frandom(4,8))
		TNT1 A 0 A_GiveToTarget("Points",800)
		Stop
	Death.JazzprojIce:
		TNT1 A 0 A_GiveToTarget("Points",800)
		ELM3 A 5 A_GenericFreezeDeath
		ELM3 A 1 A_FreezeDeathChunks
		Wait
	Death.JazzprojFire:
	Burn:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_GiveToTarget("Points",800)
		TNT1 A 0 A_PlaySound("FMTRFIRE")
		ELM3 A 2 A_SpawnItemEx("FiredeadSpawner",0,0,0,0,0,0,0,128)
		TNT1 A 2 A_SpawnItemEx("Firedead",0,0,0,0,0,0,0,128)
		TNT1 A 0 A_SpawnItemEx("AshHolm",0,0,0,0,0,0,0,128)
		Stop
	}
}