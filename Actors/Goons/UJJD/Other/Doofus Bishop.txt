Actor ElementGoon4 4036
{
	//$Category JJ_Goons
	Tag "Doofus Bishop"
	Health 500
	Radius 31
	Height 96
	Mass 100000
	Speed 10
	PainChance 128
	MONSTER
	+FLOAT
	+FLOATBOB
	+NOGRAVITY
	+NOBLOOD
	+NOINFIGHTING
	+COUNTKILL
	+DONTHARMCLASS
	Obituary "OB_ELEGO"
	States
	{
	Spawn:
		ELM4 AB 1 A_Look
		Loop
	See:
		ELM4 AB 1 A_Chase
		Loop
	Missile:
		ELM4 C 20 A_FaceTarget
		ELM4 D 4 
		ELM4 E 4 
		ELM4 F 4 BRIGHT A_CustomMissile("MagicAttack",32,0,0,0,0)
		ELM4 E 4
		ELM4 D 4
		ELM4 C 4
		Goto See
	Death: 
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("JazzPoof",0,0,24,frandom(-8,8),frandom(-8,8),frandom(4,8))
		TNT1 A 0 A_GiveToTarget("Points",1000)
		Stop
	Burn:
	Death.JazzprojFire:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_GiveToTarget("Points",1000)
		TNT1 A 0 A_PlaySound("FMTRFIRE")
		ELM4 A 2 A_SpawnItemEx("FireDeadSpawner",0,0,0,0,0,0,0,128)
		TNT1 A 2 A_SpawnItemEx("FireDead",0,0,0,0,0,0,0,128)
		TNT1 A 0 A_SpawnItemEx("AshHolm",0,0,0,0,0,0,0,128)
		Stop
	Death.JazzprojIce:
		TNT1 A 0 A_GiveToTarget("Points",1000)
		ELM4 A 5 A_GenericFreezeDeath
		ELM4 A 1 A_FreezeDeathChunks
		Wait
	}
}

Actor MagicAttack //double Damagetype assigned - ozy81
{
	Radius 8
	Height 8
	Speed 30
	Damage 5
	DamageType JazzProj
	DamageType Disintegrate
	RenderStyle Add
	Alpha 0.75
	Projectile
	+BRIGHT
	+RANDOMIZE
	DeathSound "BLSTRHIT"
	States
	{
	Spawn:
		ELMP AB 6
		TNT1 A 0 A_PlaySound("WITCHMAG")
		Loop
	Death:
		ELMH ABCDEF 3
		Stop
	}
}