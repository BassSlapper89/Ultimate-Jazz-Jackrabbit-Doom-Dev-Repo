Actor MechBoss 31526
{
	//$Category Goons
	Tag "Turtlenator"
	Health 5000
	Radius 30
	Height 80
	Mass 100000
	Speed 15
	PainChance 0
	DamageFactor "mechboss", 0
	MinMissileChance 160
	MeleeRange 32
	MeleeDamage 44
	MONSTER
	+FLOORCLIP
	+SOLID
	+NOINFIGHTING
	+NOBLOOD
	+BOSS
	+COUNTKILL
	+DONTHARMSPECIES
	SeeSound "mechsee"
	PainSound "mechpain"
	DeathSound "mechpain"
	ActiveSound "mechstep"
	Obituary "$OB_NATOR"
	States
	{
	Spawn:
		MBSS A 10 A_Look
		Loop
	See:
		MBSS AABBCCDD 3 A_Chase
		MBSS A 0 A_PlaySoundEx("mechstep", 1)
		Loop
	Missile:
		MBSS F 20 A_FaceTarget
		MBSS G 2 Bright A_CustomMissile("MechBossHeatSeek",32,32,0,0,0)
		MBSS F 10 A_FaceTarget
		MBSS G 2 Bright A_CustomMissile("MechBossHeatSeek",32,32,0,0,0)
		MBSS F 10 A_FaceTarget
		MBSS G 2 Bright A_CustomMissile("MechBossHeatSeek",32,32,0,0,0)
		MBSS I 10 A_FaceTarget
		MBSS H 4 Bright A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS H 4 Bright A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS H 4 Bright A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS I 10 A_FaceTarget
		MBSS h 4 Bright A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS H 4 Bright A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS H 4 Bright A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS i 10 A_FaceTarget
		MBSS h 4 Bright A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS H 4 Bright A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS H 4 Bright A_CustomMissile("MechBossProj",32,-32,0,0,0)
		MBSS A 20 A_FaceTarget
		MBSS J 20 A_PlaySound ("mbfgfire")
		MBSS K 10 Bright
		MBSS L 20 Bright A_CustomMissile("MechBFGBall",32,0,0,0,0)
		MBSS J 20 A_FaceTarget
		Goto See
	Melee:
		TNT1 A 0 A_PlaySound
		MBSS K 10 A_FaceTarget
		MBSS L 5 A_SargAttack
		Goto see
	Death:
		TNT1 A 0 A_Stopsound(CHAN_VOICE)
		MBSS m 0 A_SpawnItemEx("JetpackZombieExplosion", Random(-15, 15), Random(-15, 15), Random(20, 30), 0, 0, 0, 0, 128)
		MBSS m 2
		MBSS m 2 A_SpawnItemEx("JetpackZombieExplosion", Random(-20, 20), Random(-20, 20), Random(20, 40), 0, 0, 0, 0, 128)
		MBSS m 2
		MBSS m 2 A_SpawnItemEx("JetpackZombieExplosion", Random(-20, 20), Random(-20, 20), Random(20, 40), 0, 0, 0, 0, 128)
		MBSS m 2
		MBSS m 2 A_SpawnItemEx("JetpackZombieExplosion", Random(-20, 20), Random(-20, 20), Random(20, 40), 0, 0, 0, 0, 128)
		BMCH AAAAAAAAAAA 0 A_SpawnItemEx("JetpackZombieExplosion", Random(-30, 30), Random(-30, 30), Random(20, 40), 0, 0, 0, 0, 128)
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("JazzPoof",0,0,24,frandom(-8,8),frandom(-8,8),frandom(4,8))
		TNT1 A 0 A_GiveToTarget ("Points",6000)
		Stop
	}
}

Actor MechBossProj : ArachnotronPlasma
{
	Alpha 0.9
	SeeSound "GoonPLAS"
	DeathSound "PlasmaFI"
	DamageType Mechboss
	Height 8
	+DONTHURTSPECIES
	States
	{
	Spawn:
		MPLS AB 3 Bright
		Loop
	Death:
		MPLS CDEFGHI 3 Bright
		Stop
	}
}

Actor MechBossHeatSeek
{
	Radius 11
	Height 8
	Speed 15
	Damage 2
	Projectile
	DamageType Mechboss
	+SEEKERMISSILE
	+RANDOMIZE
	+DEHEXPLOSION
	+ROCKETTRAIL
	+SCREENSEEKER
	+DONTHURTSPECIES
	DeathSound "SEEKXPLD"
	States
	{
	Spawn:
		HRKT A 1 bright A_SeekerMissile(9,6,SMF_LOOK|SMF_CURSPEED,75,10)
		HRKT A 0 A_SpawnItemEx ("MissileTrail", -21, 0, 0, 1)
		Loop
	Death:
		SEKE AB 3 bright
		SEKE C 3 bright A_Explode (4,32,0)
		SEKE DEFGHIJKL 3 bright
		Stop
	}
}

Actor MechBFGBall
{
	Radius 13
	Height 8
	Speed 25
	Damage 8
	Projectile
	+BRIGHT
	+RANDOMIZE
	+DONTHURTSPECIES
	RenderStyle Add
	DamageType Mechboss
	DamageType DarkShellBoss
	Alpha 0.75
	SeeSound "mbfgfire"
	DeathSound "mbfgdie"
	States
	{
	Spawn:
		MBLL AB 2
		Loop
	Death:
		MBDD ABCD 4
		MBDD AB 4 A_SpawnItemEX("MechBFGBallExtra",0,0,0,0,0)
		MBDD BCD 4
		Stop
	}
}

Actor MechBFGBallExtra
{
	+BRIGHT
	+NOBLOCKMAP
	+NOGRAVITY
	RenderStyle Add
	Alpha 0.75
	DamageType BFGSplash
	States
	{
	Spawn:
		MBEX ABCD 8
		Stop
	}
}