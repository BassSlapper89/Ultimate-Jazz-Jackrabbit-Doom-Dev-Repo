Actor JJASaurian 3851
{
	//$Category JJ_Goons
	Tag "Saurian Mercenary"
	Health 100
	Radius 16
	Height 56
	Mass 100000
	Speed 15
	PainChance 32
	MONSTER
	+FLOORCLIP
	+SOLID
	+NOINFIGHTING
	+NOBLOOD
	+COUNTKILL
	+DONTHARMSPECIES
	SeeSound "saursee"
	PainSound "saurow"
	DeathSound "saurow"
	Obituary "$OB_SAUME"
	States
	{
	Spawn:
		SAUR B 10 A_Look
		Loop
	See:
		SAUR AABBCCDD 3 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_Jump(256, "Missile1", "Missile2")
	Missile1:
		SAUR DDDDD 2 A_FaceTarget
		SAUR E 0 A_PlaySound("saurgun", CHAN_WEAPON)
		SAUR F 2 BRIGHT A_CustomBulletAttack(8, 8, 1, 5, "JazzFirePuff")
		SAUR E 2 A_FaceTarget
		//SAUR E 0 A_PlaySound("saurgun", CHAN_WEAPON)
		SAUR F 2 BRIGHT A_CustomBulletAttack(8, 8, 1, 5, "JazzFirePuff")
		SAUR E 2 A_FaceTarget
		//SAUR E 0 A_PlaySound("saurgun", CHAN_WEAPON)
		SAUR F 2 BRIGHT A_CustomBulletAttack(8, 8, 1, 5, "JazzFirePuff")
		SAUR E 2 A_FaceTarget
		SAUR E 0 A_SentinelRefire
		Goto See
	Missile2:
		SAUR DDDDD 2 A_FaceTarget
		SAUR G 4
		SAUR H 4  A_CustomMissile("SaurianBomb",32,0,0,0,0)
		Goto See
	Pain:
		SAUR D 0
		Goto Missile2
	Death:
		SAUR J 4
		SAUR K 4 A_Scream
		SAUR L 4 A_NoBlocking
		SAUR M 4
		SAUR N 4
		SAUR O 4
		SAUR P -1
		Stop
		TNT1 A 0 A_GiveToTarget("Points",400)
		Stop
	Death.JazzprojFire:
	Burn:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_GiveToTarget("Points",400)
		TNT1 A 0 A_PlaySound("FMTRFIRE")
		SAUR JJJ 2 A_SpawnItemEx("FireDeadSpawner",0,0,0,0,0,0,0,128)
		TNT1 A 2 A_SpawnItemEx("FireDead",0,0,0,0,0,0,0,128)
		TNT1 A 0 A_SpawnItemEx("AshHolm",0,0,0,0,0,0,0,128)
		Stop
	Death.JazzprojIce:
		TNT1 A 0 A_GiveToTarget("Points",400)
		SAUR J 5 A_GenericFreezeDeath
		SAUR J 1 A_FreezeDeathChunks
		Wait
	}
}

Actor SaurianBomb
{
	Radius 16
	Height 16
	Speed 25
	Projectile
	+HEXENBOUNCE
	-NOGRAVITY
	+EXPLODEONWATER
	BounceFactor 0.8
	BounceCount 6
	ReactionTime 139
	Gravity 0.15
	DeathSound "SEEKXPLD"
	BounceSound "saurgren"
	SeeSound "BMBTHROW"
	Obituary "$OB_SAUGR"
	DamageType Grenade
	States
	{
	Spawn:
		SBMB ABCDEFGH 2 BRIGHT
		Loop
	Death:
		RFLE AB 3 BRIGHT A_NOGRAVITY
		RFLE C 3 BRIGHT A_Explode (20,64,0)
		RFLE DEFGHIJK 3 BRIGHT
		Stop
	Grenade:
		SBMB ABCDEFGH 1000 A_Die
		Wait
	}
}