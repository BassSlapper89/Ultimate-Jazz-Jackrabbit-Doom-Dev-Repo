Actor Lizard 30025
{
	//$Category JJ_Goons
	Tag "Flying Lizard Goon"
	Health 75
	Radius 20
	Height 56
	Speed 8
	PainChance 0
	Mass 200
	MONSTER
	+FLOAT
	+NOGRAVITY
	+FLOATBOB
	+NOBLOOD
	+DONTFALL
	+NOINFIGHTING
	+COUNTKILL
	+DONTHARMSPECIES
	SeeSound "LIZRDSEE"
	PainSound "LIZRDPAI"
	ActiveSound "LIZRDACT"
	Obituary "$OB_LIZAR"
	States
	{
	Spawn:
		FLIZ A 0 A_PlaySound("HELILoop")
		FLIZ ABC 3 A_Look
		Loop
	See:
		FLIZ A 0 A_PlaySound("HELILoop")
		FLIZ ABC 3 A_Chase
		FLIZ A 0 A_PlaySound("HELILoop")
		FLIZ DAB 3 A_Chase
		FLIZ A 0 A_PlaySound("HELILoop")
		FLIZ CDA 3 A_Chase
		FLIZ A 0 A_PlaySound("HELILoop")
		FLIZ BCD 3 A_Chase
		Loop
	Missile:
		FLIZ A 0 A_PlaySound("HELILoop")
		FLIZ BCD 3 A_FaceTarget
		FLIZ A 0 A_PlaySound("HELILoop")
		FLIZ E 3 A_FaceTarget
		FLIZ F 3 A_FaceTarget
		FLIZ G 3 A_CustomMissile("LizardBomb")
		FLIZ A 0 A_PlaySound("HELILoop")
		FLIZ D 3 
		Goto See
	Death:
		TNT1 A 0 A_StopSound(CHAN_VOICE)
		FLIZ A 0 A_SpawnItemEx("WalkingLizard",0,0,8,0,0,16)
		FLIZ A 0 A_SpawnItemEx("LizardPropeller",0,0,8,0,0,16)
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("JazzPoof",0,0,24,frandom(-8,8),frandom(-8,8),frandom(4,8))
		TNT1 A 0 A_GiveToTarget("Points",200)
		Stop
	Death.JazzprojIce:
		TNT1 A 0 A_StopSound(CHAN_VOICE)
		TNT1 A 0 A_GiveToTarget("Points",200)
		FLIZ A 5 A_GenericFreezeDeath
		FLIZ A 1 A_FreezeDeathChunks
		Wait
	Death.JazzprojFire:
	Burn:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_StopSound(CHAN_VOICE)
		TNT1 A 0 A_GiveToTarget("Points",200)
		TNT1 A 0 A_PlaySound("FMTRFIRE")
		FLIZ AAA 2 A_SpawnItemEx("FireDeadSpawner",0,0,0,0,0,0,0,128)
		FLIZ A 0 A_SpawnItemEx("LizardPropeller",0,0,8,0,0,16)
		TNT1 A 2 A_SpawnItemEx("FireDead",0,0,0,0,0,0,0,128)
		TNT1 A 0 A_SpawnItemEx("AshHolm",0,0,0,0,0,0,0,128)
		Stop
	}
}

Actor LizardBomb 
{
	Radius 6
	Height 6
	Speed 12
	Damage 6
	Gravity 0.1
	ExplosionDamage 32
	ExplosionRadius 96
	Projectile
	-NOGRAVITY
	+BRIGHT
	DeathSound "SEEKXPLD"
	SeeSound "BMBTHROW"
	States
	{
	Spawn:
		BOMB ABCDEFGHIJKL 1
		Loop
	Death:
		SBLL A 3
		BOMB M 0 A_SetTranslucent (0.75,1)
		BOMB M 3 A_Explode
		BOMB NOPQRS 3
		Stop
	}
}

Actor LizardPropeller
{
	Radius 12
	Height 32
	Speed 15
	Damage 8
	Projectile
	States
	{
	Spawn:
		PRPL ABCD 3 A_PlaySound("HELILoop")
		Loop
	Death:
		TNT1 A 0 A_StopSound(CHAN_VOICE)
		TNT1 A 0 A_PlaySound("SEEKXPLD")
		SBLL A 3 BRIGHT
		BOMB M 0 BRIGHT A_SetTranslucent (0.75,1)
		BOMB M 3 BRIGHT A_Explode(32,96)
		BOMB NOPQRS 3 BRIGHT
		Stop
	}
}