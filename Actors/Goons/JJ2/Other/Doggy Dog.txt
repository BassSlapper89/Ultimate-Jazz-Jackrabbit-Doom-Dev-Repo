Actor JJDog 3128
{
	//$Category JJ_Goons
	Tag "Doggy Dog"
	Radius 10
	Height 24
	Health 100
	Damage 20
	Speed 25
	PainChance 0
	Mass 4000
	MONSTER
	+COUNTKILL
	+DONTHARMSPECIES
	+JUMPDOWN
	+NOBLOOD
	+NOINFIGHTING
	ActiveSound "doggyact"
	AttackSound "DOGGYATK"
	SeeSound "doggysee"
	Obituary "$OB_JJDOG"
	States
	{
	Spawn:
		DOGS A 10 A_Look
		Loop
	See:
		DOGS ABCD 3 A_Chase
		Loop
	Melee:
		DOGS AB 8 A_FaceTarget
		DOGS C 8 A_CustomMeleeAttack(5)
		Goto See
	Death:
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("JazzPoof",0,0,24,frandom(-8,8),frandom(-8,8),frandom(4,8))
		TNT1 A 0 A_GiveToTarget("Points",100)
		Stop
	Burn:
	Death.JazzprojFire:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_GiveToTarget("Points",100)
		TNT1 A 0 A_PlaySound("FMTRFIRE")
		DOGS CCC 2 A_SpawnItemEx("FireDeadSpawner",0,0,0,0,0,0,0,128)
		TNT1 A 2 A_SpawnItemEx("FireDead",0,0,0,0,0,0,0,128)
		TNT1 A 0 A_SpawnItemEx("AshHolm",0,0,0,0,0,0,0,128)
		Stop
	Death.JazzprojIce:
		TNT1 A 0 A_GiveToTarget("Points",100)
		DOGS C 5 A_GenericFreezeDeath
		DOGS C 1 A_FreezeDeathChunks
		Wait
	}
}