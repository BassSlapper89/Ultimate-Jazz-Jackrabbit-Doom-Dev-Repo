Actor Froog 3798
{
	//$Category Goons
	Health 70
	Radius 16
	Height 48
	Speed 10
	PainChance 0
	Mass 1000
	DamageFactor "MuckSnot", 0
	DamageFactor "MuckSnot2", 0
	MONSTER
	+COUNTKILL
	+DONTHARMSPECIES
	+FLOORCLIP
	+NOBLOOD
	+NOINFIGHTING
	SeeSound "Turtsight"
	PainSound "Turtpain"
	ActiveSound "Turtactive"
	Obituary "$OB_FROOG"
	States
	{
	Spawn:
		FRGG ABCDEFGHIJKL 3 A_Look
		Loop
	See:
		FRGG ABCDEFGHIJKL 3 A_Chase
		Loop
	Melee:
		FRGG ABCD 4 A_FaceTarget
		FRGG E 3 A_PlaySound("BMBThrow")
		FRGG ABCD 4 A_FaceTarget
		FRGG E 3 A_SkelFist
		Goto See
	Death:
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("JazzPoof",0,0,24,frandom(-8,8),frandom(-8,8),frandom(4,8))
		TNT1 A 0 A_GiveToTarget("Points",150)
		Stop
	Death.JazzprojFire:
	Burn:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_GiveToTarget("Points",150)
		TNT1 A 0 A_PlaySound("FMTRFIRE")
		FRGG AAA 2 A_SpawnItemEx("FireDeadSpawner",0,0,0,0,0,0,0,128)
		TNT1 A 2 A_SpawnItemEx("FireDead",0,0,0,0,0,0,0,128)
		TNT1 A 0 A_SpawnItemEx("AshHolm",0,0,0,0,0,0,0,128)
		Stop
	Death.JazzprojIce:
		TNT1 A 0 A_GiveToTarget("Points",150)
		FRGG A 5 A_GenericFreezeDeath
		FRGG A 1 A_FreezeDeathChunks
		Wait
	}
}