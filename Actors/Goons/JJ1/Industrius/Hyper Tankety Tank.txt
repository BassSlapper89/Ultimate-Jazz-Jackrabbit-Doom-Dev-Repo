Actor HyperTank 3212
{
	//$Category JJ_Goons
	Tag "Hyper Tankety Tank"
	Radius 16
	Height 48
	Speed 10
	Health 150
	PainChance 156
	Mass 600
	Damage 3
	MONSTER
	+COUNTKILL
	+DONTHARMSPECIES
	+NOBLOOD
	+NOINFIGHTING
	+SOLID
	SeeSound "TURTSIGHT"
	PainSound "TURTPAIN"
	ActiveSound "TURTACTIVE"
	Obituary "$OB_HTANK"
	States
	{
	Spawn:
		TANK A 4 A_Look
		Loop
	See:
		TANK A 4 A_StartSound("TANKSTRT")
		TANK A 0 A_PlaySoundEx("TANKLoop", "SoundSlot7", 1)
		TANK A 3 A_Chase
		TANK A 0 A_PlaySoundEx("TANKLoop", "SoundSlot7", 1)
		TANK B 3 A_Chase
		TANK A 0 A_PlaySoundEx("TANKLoop", "SoundSlot7", 1)
		TANK C 3 A_Chase
		TANK A 0 A_PlaySoundEx("TANKLoop", "SoundSlot7", 1)
		TANK B 3 A_Chase
		Goto See+1
	Missile:
		FTTG A 0 A_PlaySoundEx("TANKLoop", "SoundSlot7", 1)
		TANK A 2 A_FaceTarget
		TANK A 4 A_FaceTarget
		TANK E 4 A_CustomMissile("FanolintRocket",32,0,0,0,0)
		Goto See+1
	Death:
		TNT1 A 0 A_StopSound(CHAN_VOICE)
		TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("JetpackZombieExplosion", Random(-30, 30), Random(-30, 30), Random(20, 40), 0, 0, 0, 0, 128)
		TNT1 AAAAAAA 0 A_SpawnItemEx("JazzPoof",0,0,random(8,64),frandom(-5,5),frandom(-5,5),frandom(2,9))
		TNT1 A 0 A_GiveToTarget("Points",400)
		Stop
	}
}

Actor JazzMissile 3210 //obituaries can't be used here, they are triggered by Minissiles themselves - ozy81
{
	//$Category JJ_Hazards
	Radius 8
	Height 16
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
		INDM A 0
		TNT1 A 64
		TNT1 A 0 A_StartSound("RFLAUNCH")
		TNT1 B 1 A_SpawnItemEx("Minissiles",16,0,20,15)
		Loop
	}
}

Actor JazzMissile2 : JazzMissile 3211
{
	//$Category JJ_Hazards
	States
	{
	Spawn:
		INDM A 0
		TNT1 A 96
		TNT1 A 0 A_StartSound("RFLAUNCH")
		TNT1 B 1 A_SpawnItemEx("Minissiles",16,0,20,15)
		Loop
	}
}