Actor EvilJazz 32527
{
	//$Category Goons
	Tag "Evil Jazz"
	Health 3000
	Radius 32
	Height 128
	Mass 1000000
	Speed 25
	Scale 2.0
	PainChance 0
	MONSTER
	+BOSSDEATH
	+COUNTKILL
	+DONTHARMSPECIES
	+FLOORCLIP
	+NOBLOOD
	+NOINFIGHTING
	Obituary "$OB_EVILJ"
	States
	{
	Spawn:
		EJAZ AABB 3 A_Look
		Goto See
	See:
		EJAZ AABBCCFF 3 A_Chase
		Loop
	Melee:
	Missile:
		EJAZ DE 1 A_Chase("","")
		EJAZ D 0 BRIGHT A_PlaySound("vile/firecrkl")
		EJAZ DE 3 BRIGHT A_FaceTarget
		EJAZ D 1 BRIGHT A_CustomMissile("VileFireSpawner",0,0,15,0,0)
		EJAZ E 1 BRIGHT A_CustomMissile("VileFireSpawner",0,0,0,0,0)
		EJAZ D 2 BRIGHT A_CustomMissile("VileFireSpawner",0,0,-15,0,0)
		EJAZ E 10 BRIGHT
		Goto See
	Death:
		EJAZ A 0 A_StopSound
		EJAZ A 0 A_Scream
		EJAZ A 2 ThrustThingZ(0, random(2, 8), 0, 0)
		EJAZ A 0 ThrustThing(random(0, 255), random(2, 8), 0, 0)
		EJAZ A 2 A_NoBlocking
		EJAZ A 2
		EJAZ A 0 A_ChangeFlag("FloatBob", 0)
		EJAZ A 0 A_StopSoundEx("JetpackZombie/Fly")
		EJAZ A 0 A_SpawnItemEx("JetpackZombieExplosion", Random(-15, 15), Random(-15, 15), Random(20, 30), 0, 0, 0, 0, 128)
		EJAZ A 2
		EJAZ A 2 A_SpawnItemEx("JetpackZombieExplosion", Random(-20, 20), Random(-20, 20), Random(20, 40), 0, 0, 0, 0, 128)
		EJAZ A 2
		EJAZ A 2 A_SpawnItemEx("JetpackZombieExplosion", Random(-20, 20), Random(-20, 20), Random(20, 40), 0, 0, 0, 0, 128)
		EJAZ A 2
		EJAZ A 2 A_SpawnItemEx("JetpackZombieExplosion", Random(-20, 20), Random(-20, 20), Random(20, 40), 0, 0, 0, 0, 128)
		EJAZ AAAAAAAAAAA 0 A_SpawnItemEx("JetpackZombieExplosion", Random(-30, 30), Random(-30, 30), Random(20, 40), 0, 0, 0, 0, 128)
		TNT1 A 0 A_GiveToTarget("Points",5000)
		Stop
	}
}

Actor VileFireSpawner
{
	Radius 8
	Height 8
	Speed 15
	Damage 4
	MissileType VileFireD3
	MissileHeight 0
	ReactionTime 25
	DontHurtShooter //deprecated
	Projectile
	+RIPPER
	+FLOORHUGGER
	+BLOODLESSIMPACT
    Obituary "$OB_VILEF"
	States
	{
	Spawn:
		TNT1 A 0 A_PlaySound("vile/firecrkl")
		TNT1 A 3 BRIGHT A_SpawnItemEx("VileFireD3",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_Explode(16,64,0)
		TNT1 A 0 A_CountDown
		Loop
	Death:
		TNT1 A 6
		Stop
	}
}

Actor VileFireD3
{
	Translation "160:167=112:127"
	Radius 10
	Height 78
	Damage 8
	RenderStyle Add
	DontHurtShooter //deprecated
	+BRIGHT
	+FLOORHUGGER
	+NOCLIP
	States
	{
	Spawn:
		FIRE ABCDEFGH 4
		Stop
	}
}