Actor HipHopCage
{
	//$Category JJ_Special
	Tag "Hip Hop Cage"
	Radius 16
	Height 32
	Health 1
	Mass 1000000
	+NOBLOOD
	+SHOOTABLE
	+SOLID
	States
	{
	Spawn:
		HHOP WY 4
		Loop
	Death:
		HHOP Z -1 A_SpawnItemEx("HipHop")
		Stop
	}
}

Actor HipHop //to be tweaked with zscript soon - ozy81
{
	//$Category JJ_Special
	Tag "Hip Hop"
	Health 300
	Radius 16
	Height 16
	Mass 10000
	Speed 15
	Damage 6
	PainChance 0
	DamageFactor 1.0
	DamageFactor "JazzProj", 0
	DamageFactor "JazzProjFire", 0
	DamageFactor "JazzProjIce", 0
	+BRIGHT
	+DONTFALL
	+FLOAT
	+FRIENDLY
	+LOOKALLAROUND
	+MISSILEMORE
	+NOBLOOD
	+NOGRAVITY
	+NOICEDEATH
	+NOTELEPORT
	+SHOOTABLE
	+SOLID
	SeeSound "HIPHOPAT"
	Obituary "$OB_HIPOP"
	States
	{
	Spawn:
		HHOP ABCD 3 A_Look
		Loop
	See:
		HHOP ABCD 3 A_Chase
		Loop
	Missile:
		HHOP A 8 A_Jump(150, "Missile2")
		HHOP B 3 A_FaceTarget
		HHOP CD 3 A_SkullAttack
		Goto See
	Missile2:
		HHOP CD 3 A_FaceTarget
		HHOP CD 4 A_FaceTarget
		HHOP CD 8 A_CustomMissile("HipHopProj",0,0,5,0,0)
		HHOP CD 10
		Goto See
	Death:
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("JazzPoof",0,0,24,frandom(-8,8),frandom(-8,8),frandom(4,8))
		Stop
	}
}

Actor HipHopProj
{
	Radius 6
	Height 8
	Speed 10
	Damage 6
	Gravity 0.25
	RenderStyle Add
	Alpha 1
	Projectile
	+BRIGHT
	+RANDOMIZE
	SeeSound "HIPHOPAT"
	DeathSound "BLSTRHIT"
	States
	{
	Spawn:
		HHPJ A 6
		Loop
	Death:
		JBLS EFGH 4
		Stop
	}
}