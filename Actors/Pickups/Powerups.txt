actor RapidFire : CustomInventory  32538
{
  Tag "Rapid Fire"
  Inventory.PickupMessage "\cnRapid Fire!"
  Inventory.Amount 1
  Inventory.PickupSound "RAPIDFIR"
  FloatBobStrength 0.5
  -COUNTITEM
  +FLOATBOB
   +NOGRAVITY
   +INVENTORY.ALWAYSPICKUP
  states
  {
  Spawn:
	PLSP ABCDEFGH 3 Bright
	loop
  Pickup:
  TNT1 A 0 A_SpawnItemex("Itemsparkle")
 	TNT1 A 0 A_GiveInventory("RapidfireCount")
	Stop
  }
}

ACTOR RapidfireCount : Inventory 
{
    Inventory.PickupSound ""
	Inventory.Pickupmessage ""
	Inventory.MaxAmount 8
	inventory.Interhubamount 8
	States
	{
	Spawn:
		TNT1 A -1
		Stop
	}
}

actor JJHoverBoard : PowerupGiver 32002
{
	Tag "Hoverboard"
	inventory.pickupmessage "\cfYou got a \ctHoverboard\cf!"
	inventory.maxamount 0
	powerup.duration -30
	powerup.color "FF 00 FF", 0.1
	Inventory.PickupSound "Powerup"
	powerup.type Flight
	-COUNTITEM
	FloatBobStrength 0.5
	+FLOATBOB
	 +NOGRAVITY
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Spawn:
		FLYT ABCDEDCBA 3
		Loop
		  Pickup:
		  TNT1 A 0 A_SpawnItemex("Itemsparkle")
		  TNT1 AAAAAAAAAA 0 A_SpawnItemEx("ItemSparklePower",0,0,24,frandom(-8,8),frandom(-8,8),frandom(4,8))
	Stop
	}
}

Actor JJInvul : CustomInventory 3136
{
  Tag "Invulnerability"
  Inventory.PickupSound "INVINCE"
  Inventory.PickupMessage "\cfInvulnerability!"
  Renderstyle Translucent
  Alpha 0.9
  +FLOATBOB
   +NOGRAVITY
   FloatBobStrength 0.5
  -COUNTITEM
  States
  {
  Spawn:
    INVS ABCDCB 1 Bright
    Loop
  Pickup:
	TNT1 A 0
	{
		A_GiveInventory("JJTimer",30);
		if (!CountInv("PowerInvulnerable"))
		{
			A_SpawnItemEx("JJManager",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER);
			A_SpawnItemEx("JJEffectSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER);
		}
		A_SetBlend("Yellow",1.0,20,"Yellow");
		A_GiveInventory("JJInvuln");
	}
	TNT1 A 0 A_SpawnItemex("Itemsparkle")
	TNT1 AAAAAAAAAA 0 A_SpawnItemEx("ItemSparklePower",0,0,24,frandom(-8,8),frandom(-8,8),frandom(4,8))
	Stop
	}
}

Actor JJInvuln : PowerupGiver
{
  Powerup.Color "00 00 00", 0.0
  Powerup.Type "Invulnerable"
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
}

Actor JJTimer : Inventory
{
  Inventory.MaxAmount 30
  Inventory.InterhubAmount 0
}

Actor JJEffectSpawner
{
  +NOINTERACTION
  States
  {
  Spawn:
	TNT1 A 1 Light("InvulnActive") 
	{
		if (CountInv("PowerInvulnerable",AAPTR_MASTER))
		{
			if(A_CheckFlag("CORPSE","Null",AAPTR_MASTER)) { return state("Null"); }
			A_SpawnParticle("Yellow",SPF_FULLBRIGHT,35,2,0,random(-30,30),random(-30,30),random(0,64),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(0,3),0,0,0,1,-1,-0.1);
			A_SpawnParticle("Yellow",SPF_FULLBRIGHT,35,2,0,random(-30,30),random(-30,30),random(0,64),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(0,3),0,0,0,1,-1,-0.1);
			A_Warp(AAPTR_MASTER,0,0,0,0,WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION);
			return state("");
		}
		return state("Null");
	}
	Wait
	}
}

Actor JJManager
{
  +NOINTERACTION
  +NOTONAUTOMAP
  +NOTIMEFREEZE
  var int user_time;
  States
  {
  Spawn:
	TNT1 A 35 
	{
		if (A_CheckFlag("CORPSE","Null",AAPTR_MASTER)) { return state("Null"); }
		if (CountInv("JJTimer",AAPTR_MASTER))
		{
			user_time = CountInv("JJTimer",AAPTR_MASTER);
			A_TakeInventory("JJTimer",1,0,AAPTR_MASTER);
		}
		else
		{
			user_time--;
		}		
		if (user_time == 4 || user_time == 3 || user_time == 2)
			{
			if (user_time == 4)
			{
				A_PlaySound("INVINCE",5,1,0,ATTN_NONE);
			}
			A_PlaySound("INVINCE",5,1,0,ATTN_NONE);
			A_GiveInventory("JJFade",1,AAPTR_MASTER);
		}			
		if (user_time <= 0)
		{
			A_TakeInventory("JJTimer",30,0,AAPTR_MASTER);
			return state("Null");
		}
		return state("");
	}
	Wait
	}
}

Actor JJFade : CustomInventory
{
  +INVENTORY.ALWAYSPICKUP
  States
  {
  Pickup:
	TNT1 A 0 A_SetBlend("Yellow",0.4,12)
	Stop
	}
}

actor TurboTime : PowerupGiver 3924  //HasteSphere by scalliano
{
	Game Doom
	Tag "Turbocharge"
	inventory.pickupmessage "\cfTurbocharge!"
	inventory.maxamount 0
	powerup.duration -30
	powerup.color "FF FF 00", 0.1
	powerup.type Speed
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Spawn:
		HAST ABCD 1 Bright
		Loop
	Death:
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("Jazzpoof",0,0,0,frandom(-8,8),frandom(-8,8),frandom(4,8))
		Stop
	}
}

ACTOR TurboTimeBox 3925
{
  Height 30
  Radius 16
  Health 1
  Damagefactor "Normal", 0
  Damagefactor "Ice", 0
  Damagefactor "Fire", 0
  Damagefactor "JAZZPROJ", 1
  Damagefactor "JAZZPROJFire", 1
  Damagefactor "JazzProjIce", 1
  +Shootable
  +SOLID
  +Noblood
  States
  {
  Spawn:
    HAST ABCD 1 Bright
    Loop
  Death:
  TNT1 A 0 A_PlaySound ("WEAPNGET")
  TNT1 A 0 A_Print("It's Turbo Time!", 3, "Smallfont")
  TNT1 A 0 A_GiveToTarget("TurboTime", 1, AAPTR_DEFAULT)
  TNT1 AAAAAAAAAA 0 A_SpawnItemEx("Jazzpoof",0,0,8,frandom(-8,8),frandom(-8,8),frandom(4,8))
	Stop
  }
}