#library "dizzy"
#include "zcommon.acs"

//ORIGINAL CODE BY MAXICLOUDS from RE-BLOOD, VERY MINOR MODIFICATIONS BY OZYMANDIAS81

int playerID;
int playerNum;
global int 1:dizzy;
global int 2:dizzyTimer;
global int 3:dizzyAng1;
global int 4:dizzyAng2;
global int 5:dizzyAng3;

Script "InitJazzEffects" ENTER //should be added on your main acs library if there is one --ozy81
{
	playerNum = PlayerNumber(); //maybe useful for multiplayer
	playerID = 1000 + playerNum;
	Thing_ChangeTID (0, playerID);
	ACS_NamedExecute("CaterpillarCheck", 0);
}

script "DizzyScreen" ENTER
{
	int width = 320, height = 240;
	SetHudSize(width, height, 0);
	SetFont("M_DIZZY");

	// Half width and height (in fixed-point).
	int half_width  = (width / 2) << 16;
	int half_height = (height / 2) << 16;

	while (true)
	{
		int dizzyness =  CheckInventory("CaterpillarCount");
		if (dizzyness > 1)
		{
			HudMessage(s:"A"; HUDMSG_FADEINOUT | HUDMSG_LAYER_UNDERHUD | HUDMSG_NOTWITHFULLMAP | HUDMSG_NOTWITHOVERLAYMAP | HUDMSG_ALPHA , 0, CR_UNTRANSLATED, half_width, half_height, 0.0, 0.5, dizzyness, 0.2);
		}
		
		Delay(1);
	}
}

Script "CaterpillarCheck" (void)
{
	if ( CheckActorInventory(playerID,"CaterpillarStop"))
	{
		ACS_NamedExecute("CaterpillarSmoke",0);
	}
	Delay(1);
	restart;
}

Script "CaterpillarSmoke" (void)
{
	dizzy = CheckInventory("CaterpillarCount");
	dizzy--;
	if (dizzy < 0) dizzy = 0;	
	int dtilt1 = 170;
	int dtilt2 = 170;
	int dpitch = 20;
	dizzytimer+=15;
	if (dizzy < 512)
	{
		int mul = (dizzy*65536)/512;
		dtilt1 = FixedMul(dtilt1,mul);
		dtilt2 = FixedMul(dtilt2,mul);
		dpitch = FixedMul(dpitch,mul);
	}
	int sin2 = Sin(2*dizzytimer*32)/2;
	int sin3 = Sin(3*dizzytimer*32)/2;
	int sin4 = Sin(4*dizzytimer*32)/2;
	int sin5 = Sin(5*dizzytimer*32)/2;
	int gScreenTilt = FixedMul(sin2+sin3,dtilt1);
	int dizzyTilt = FixedMul(sin3+sin4,dtilt2);
	int dizzyPitch = FixedMul(sin4+sin5,dpitch);
	int pitch = GetActorPitch(playerid);
	int roll = GetActorRoll(playerid);
	int angle = GetActorAngle(playerid);
	ChangeActorPitch(playerid,(dizzyPitch-dizzyAng1)*32+Pitch);
	ChangeActorRoll(playerid,(gScreenTilt-dizzyAng2)*32+Roll);
	ChangeActorAngle(playerid,(dizzyTilt-dizzyAng3)*32+Angle);
	dizzyAng1 = dizzyPitch;
	dizzyAng2 = gScreenTilt;
	dizzyAng3 = dizzyTilt;
	TakeInventory("CaterpillarCount", 1);
	Delay(1);
	if (CheckInventory("CaterpillarCount") < 1 )
		{
			ACS_NamedExecute("CaterpillarCheck", 0);
			terminate;
		}
	restart;
}